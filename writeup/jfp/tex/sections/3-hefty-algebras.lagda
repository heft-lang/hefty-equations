\begin{code}[hide]
module tex.sections.3-hefty-algebras where

open import tex.sections.2-algebraic-effects

open import Function hiding (_‚ü®_‚ü©_)
open import Level hiding (Lift)
open import Agda.Primitive
open import Data.Empty
open import Data.Unit
open import Data.Bool hiding (T)
open import Data.Sum
open import Data.Product
open import Data.Maybe hiding (_>>=_)
open import Data.Nat hiding (_‚äî_)
open import Data.Integer using (‚Ñ§)
open import Data.String
open import Data.List hiding ([_])
open import Data.List.Membership.Propositional
open import Data.List.Relation.Unary.Any using (here; there)
open import Relation.Binary.PropositionalEquality renaming ([_] to ‚â°[_])
open import Relation.Unary using (IUniversal ; _‚áí_)

open import Data.String

open Abbreviation using (hThrow; ‚ôØ_)

\end{code}

\section{Hefty Trees and Algebras}
\label{sec:hefty-trees-and-algebras}

\newcommand{\Mat}[1]{\mathrm{#1}}

As observed in \cref{sec:higher-order-effects}, operations such as \ad{catch}
can be elaborated into more primitive effects and handlers.  However, these
elaborations are not modular.  We solve this problem by factoring
elaborations into interfaces of their own to make them modular.

To this end, we first introduce a new type of abstract syntax trees
(\cref{sec:towards-hefty-trees,sec:lifting-algebraic-to-higher-order,sec:hefty-monadic-bind})
representing computations with higher-order operations, which we dub \emph{hefty
  trees} (an acronymic pun on \emph{h}igher-order \emph{ef}fec\emph{t}s).  We
then define elaborations as algebras (\emph{hefty algebras};
\cref{sec:hefty-algebras}) over these trees.  The following pipeline summarizes
the idea, where \ab{H} is a \emph{higher-order effect signature}:
%
\begin{equation*}
  \ad{Hefty}~\ab{H}~\ab{A}
  \xrightarrow{\Id{elaborate}} \ad{Free}~\ab{Œî}~\ab{A}
  \xrightarrow{\Id{handle}} \Id{Result}
\end{equation*}

For the categorically inclined reader, \ad{Hefty} conceptually corresponds to
the initial algebra of the functor $\Id{HeftyF}~H~A~R = A + H~R~(R~A)$ where
$H : (\ad{Set} \to \ad{Set}) \to (\ad{Set} \to \ad{Set})$ defines the signature
of higher-order operations and is a \emph{higher-order functor}, meaning we have
both the usual functorial $\Id{map} : (X \to Y) \to H~F~X \to H~F~Y$ for any
functor $F$ as well as a function
$\Id{hmap} : \Mat{Nat}(F, G) \to \Mat{Nat}(H~F, H~G)$ which lifts natural
transformations between any $F$ and $G$ to a natural transformation between
$H~F$ and $H~G$.  A hefty algebra is then an $F$-algebra over a higher-order
signature functor $H$.  The notion of elaboration that we introduce in
\cref{sec:hefty-algebras} is an $F$-algebra whose carrier is a ``first-order''
effect tree (\ad{Free}~\ab{Œî}).

In this section, we encode this conceptual framework in Agda using a
container-inspired approach to represent higher-order signature functors $H$ as
a strictly positive type.  We discuss and compare our approach with previous
work in \cref{sec:limitations}.


\subsection{Generalizing \ad{Free} to Support Higher-Order Operations}
\label{sec:towards-hefty-trees}

\begin{code}[hide]
module HeftyModule where
  open FreeModule hiding (_ùìë_; _>>_) renaming (‚Äµthrow to ‚Äµthrow‚ÇÄ)
\end{code}

As summarized in \cref{sec:free-monad}, \ad{Free}~\ab{Œî}~\ab{A} is the type of
abstract syntax trees representing computations over the effect signature
\ab{Œî}.  Our objective is to arrive at a more general type of abstract syntax
trees representing computations involving (possibly) higher-order operations.
To realize this objective, let us consider how to syntactically represent this
variant of the $\Id{censor}$ operation (\cref{sec:modularity-problem}), where
\ab{M} is the type of abstract syntax trees whose type we wish to define:
%
\begin{flalign*}
   \;\;\af{censor‚Çí‚Çö}~\as{:~(}\ad{String}~\as{‚Üí}~\ad{String}\as{)~‚Üí}~\ab{M}~\ad{‚ä§}~\as{‚Üí}~\ab{M}~\ad{‚ä§}
  &&
\end{flalign*}
%
We call the second parameter of this operation a \emph{computation parameter}.
Using \ad{Free}, computation parameters can only be encoded as continuations.
But the computation parameter of \af{censor‚Çí‚Çö} is \emph{not} a continuation, since
%
\begin{equation*}
  \ak{do}~\as{(}\af{censor‚Çí‚Çö}~~\ab{f}~~\ab{m}\as{);}~\af{‚Äµout}~\ab{s}~~\not\equiv~~\af{censor‚Çí‚Çö}~~\ab{f}~~\as{(}\ak{do}~\ab{m}\as{;}~\af{‚Äµout}~\ab{s}\as{)}.
\end{equation*}
%
The crux of the issue is how signature functors \af{‚ü¶}~\ab{Œî}~\af{‚üß}~\as{:}~\ad{Set}~\as{‚Üí}~\ad{Set} are defined.
Since this is an endofunctor on \ad{Set}, the only suitable option in the \ac{impure} constructor is to apply the functor to the type of \emph{continuations}:
%
\begin{equation*}
  \ac{impure}~\as{:~}\af{‚ü¶}~\ab{Œî}~\af{‚üß}~\as{(}\underbrace{\ad{Free}~\ab{Œî}~\ab{A}}_{\textrm{continuation}}\as{)}~\as{‚Üí}~\ad{Free}~\ab{Œî}~\ab{A}
\end{equation*}
%
A more flexible approach would be to allow signature functors to build computation trees with an \emph{arbitrary return type}, including the return type of the continuation.
A \emph{higher-order} signature functor of some higher-order signature \ab{H}, written \af{‚ü¶}~\ab{H}~\af{‚üß·¥¥}~\as{:}~\as{(}\ad{Set}~\as{‚Üí}~\ad{Set}\as{)~‚Üí}~\ad{Set}~\as{‚Üí}~\ad{Set}, would fit that bill.
Using such a signature functor, we could define the \ac{impure} case as follows:
%
\begin{equation*}
  \ac{impure}~\as{:}~\af{‚ü¶}~\ab{H}~\af{‚üß·¥¥}~\as{(}\underbrace{\ad{Hefty}~\ab{H}}_{\begin{array}{c}\text{\footnotesize computation}\\[-0.5em]\text{\footnotesize type}\end{array}}\as{)}~\overbrace{\ab{A}}^{\begin{array}{c}\text{\footnotesize continuation}\\[-0.5em]\text{\footnotesize return type}\end{array}}~\as{‚Üí}~\ad{Hefty}~\ab{H}~\ab{A}
\end{equation*}
%
Here, \ad{Hefty} is the type of the free monad using higher-order signature functors instead.
In the rest of this subsection, we consider how to define higher-order signature functors \ab{H}, their higher-order functor extensions \af{‚ü¶\_‚üß·¥¥}, and the type of \ad{Hefty} trees.

Recall how we defined plain algebraic effects in terms of \emph{containers}:
%
\begin{code}
  record Effect‚Öã : Set‚ÇÅ where
    field  Op‚Öã   : Set
           Ret‚Öã  : Op‚Öã ‚Üí Set
\end{code}
%
Here, \aF{Op} is the type of operations, and \aF{Ret} defines the return type of each operation.
In order to allow operations to have sub-computations, we generalize this type to allow each operation to be associated with a number of sub-computations, where each sub-computation can have a different return type.
The following record provides this generalization:
%
\begin{code}
  record Effect·¥¥ : Set‚ÇÅ where
    field  Op·¥¥     : Set                             -- As before
           Ret·¥¥    : Op·¥¥ ‚Üí Set                       -- As before
           Fork    : Op·¥¥ ‚Üí Set                       -- New
           Ty      : {op : Op·¥¥} (œÜ : Fork op) ‚Üí Set  -- New
\end{code}
%
The set of operations is still given by a type field (\aF{Op·¥¥}), and each operation still has a return type (\aF{Ret·¥¥}).
\aF{Fork} associates each operation with a type that indicates how many sub-computations (or \emph{forks}) an operation has, and \aF{Ty} indicates the return type of each such fork.
For example, say we want to encode an operation \ab{op} with two sub-computations with different return types, and whose return type is of a unit type.
That is, using \ab{M} as our type of computations:
%
\begin{equation*}
  \ab{op}~\as{:}~\ab{M}~\ad{‚Ñ§}~\as{‚Üí}~\ab{M}~\ad{‚Ñï}~\as{‚Üí}~\ab{M}~\ad{‚ä§}
\end{equation*}
%
The following signature declares a higher-order effect signature with a single operation with return type \ad{‚ä§}, and with two forks (we use \ad{Bool} to encode this fact), and where each fork has, respectively \ad{‚Ñ§} and \ad{‚Ñï} as return types.
%
\begin{code}[hide]
  open Effect
  open Effect·¥¥
\end{code}
\begin{code}
  example-op : Effect·¥¥
  Op·¥¥   example-op        = ‚ä§     -- A single operation
  Ret·¥¥  example-op tt     = ‚ä§     -- with return type ‚ä§
  Fork  example-op tt     = Bool  -- with two forks
  Ty    example-op false  = ‚Ñ§     -- one fork has return type ‚Ñ§
  Ty    example-op true   = ‚Ñï     -- the other has return type ‚Ñï
\end{code}
%
The extension of higher-order effect signatures implements the intuition explained above:
%
\begin{code}
  ‚ü¶_‚üß·¥¥ : Effect·¥¥ ‚Üí (Set ‚Üí Set) ‚Üí Set ‚Üí Set
  ‚ü¶ H ‚üß·¥¥ M X =
    Œ£ (Op·¥¥ H) Œª op ‚Üí (Ret·¥¥ H op ‚Üí M X) √ó ((œÜ : Fork H op) ‚Üí M (Ty H œÜ))
\end{code}
\begin{code}[hide]
  map-sig·¥¥ : ‚àÄ {H F G} ‚Üí ‚àÄ[ F ‚áí G ] ‚Üí ‚àÄ[ ‚ü¶ H ‚üß·¥¥ F ‚áí ‚ü¶ H ‚üß·¥¥ G ]
  map-sig·¥¥ Œ∏ (op , k , œà) = op , Œ∏ ‚àò k , Œ∏ ‚àò œà
\end{code}
%
Let us unpack this definition.
%
\begin{equation*}
% \af{‚ü¶}~\ab{H}~\af{‚üß}~\overbrace{\ab{M}}^{computation types}~\ab{X}~\as{=}~
  \underbrace{\ad{Œ£}~\as{(}~\aF{Op·¥¥}~\ab{H}\as{)~Œª}~\ab{op}~\as{‚Üí}}_{(1)}\as{~(}\underbrace{\aF{Ret·¥¥}~\ab{H}~\ab{op}~\as{‚Üí~}\ab{M}~\ab{X}}_{(2)}\as{)}~\ad{√ó}~\as{(}\underbrace{\as{(}\ab{œÜ}~\as{:}~\aF{Fork}~\ab{H}~\ab{op}\as{)}}_{(3)}\as{~‚Üí}~\underbrace{\ab{M}~\as{(}\aF{Ty}~\ab{H}~\ab{œÜ}\as{)}}_{(4)}\as{)}
\end{equation*}
%
The extension of a higher-order signature functor is given by (1) the sum of operations of the signature, where each operation has (2) a continuation (of type \ab{M}~\ab{X}) that expects to be passed a value of the operation's return type, and (3) a set of forks where each fork is (4) a computation that returns the expected type for each fork.

Using the higher-order signature functor and its extension above, our generalized free monad becomes:
%
\begin{code}
  data Hefty (H : Effect·¥¥) (A : Set) : Set where
    pure    : A ‚Üí Hefty H A
    impure  : ‚ü¶ H ‚üß·¥¥ (Hefty H) A ‚Üí Hefty H A
\end{code}
%
\begin{code}[hide]
  variable H H‚Ä≤ H‚Ä≥ H‚Ä¥ H‚ÇÄ H‚ÇÅ H‚ÇÇ H‚ÇÉ H‚ÇÑ : Effect·¥¥

  variable
    m n : Level
    A B C D Z : Set n
    F F‚ÇÄ F‚ÇÅ F‚ÇÇ F‚ÇÉ G : Set n ‚Üí Set (n ‚äî m)
\end{code}
\begin{code}[hide]
  infixr 12 _‚àî_

  _‚àî_ : Effect·¥¥ ‚Üí Effect·¥¥ ‚Üí Effect·¥¥
  Op·¥¥ (H‚ÇÅ ‚àî H‚ÇÇ) = Op·¥¥ H‚ÇÅ ‚äé Op·¥¥ H‚ÇÇ
  Ret·¥¥ (H‚ÇÅ ‚àî H‚ÇÇ) = [ Ret·¥¥ H‚ÇÅ , Ret·¥¥ H‚ÇÇ ]
  Fork (H‚ÇÅ ‚àî H‚ÇÇ) = [ Fork H‚ÇÅ , Fork H‚ÇÇ ]
  Ty (H‚ÇÅ ‚àî H‚ÇÇ) {inj‚ÇÅ _} œÜ = Ty H‚ÇÅ œÜ
  Ty (H‚ÇÅ ‚àî H‚ÇÇ) {inj‚ÇÇ _} œÜ = Ty H‚ÇÇ œÜ
\end{code}
%
This type of \ad{Hefty} trees can be used to define higher-order operations with
an arbitrary number of computation parameters, with arbitrary return types.
Using this type, and using a co-product for higher-order effect signatures
(\af{\_‚àî\_}) which is analogous to the co-product for algebraic effect
signatures in \cref{sec:row-insertion}, \cref{fig:censor} represents the syntax
of the \ad{censor‚Çí‚Çö} operation.
% 
\begin{figure}
\centering
\begin{minipage}{0.445\linewidth}
\begin{code}
  data CensorOp : Set where
    censor  :  (String ‚Üí String)
            ‚Üí  CensorOp
\end{code}
\end{minipage}
\hfill\vline\hfill
\begin{minipage}{0.545\linewidth}
\begin{code}
  Censor : Effect·¥¥
  Op·¥¥    Censor                 = CensorOp
  Ret·¥¥   Censor (censor f)      = ‚ä§
  Fork   Censor (censor f)      = ‚ä§
  Ty     Censor {censor f} tt   = ‚ä§
\end{code}
\end{minipage}
\\
\hrulefill\\
\begin{code}
  censor‚Çí‚Çö : (String ‚Üí String) ‚Üí Hefty (Censor ‚àî H) ‚ä§ ‚Üí Hefty (Censor ‚àî H) ‚ä§
  censor‚Çí‚Çö f m = impure (inj‚ÇÅ (censor f) , (Œª where tt ‚Üí m) , pure)
\end{code}
%
\caption{A higher-order censor effect and operation, with a single computation
  parameter (declared with \aF{Op}~\as{=}~\ad{‚ä§} in the effect signature top
  right) with return type \ad{‚ä§} (declared with \aF{Ret}~\as{=~Œª~\_~‚Üí}~\ad{‚ä§}
  top right)}
\label{fig:censor}
\end{figure}

Just like \ad{Free}, \ad{Hefty} trees can be sequenced using monadic bind.
Unlike for \ad{Free}, the monadic bind of \ad{Hefty} is not expressible in terms
of the standard fold over \ad{Hefty} trees.  The difference between \ad{Free}
and \ad{Hefty} is that \ad{Free} is a regular data type whereas \ad{Hefty} is a
\emph{nested datatype}~\citep{DBLP:journals/fac/BirdP99}.  The fold of a nested
data type is limited to describe \emph{natural transformations}.  As
\citet{DBLP:journals/fac/BirdP99} show, this limitation can be overcome by using
a \emph{generalized fold}, but for the purpose of this paper it suffices to
define monadic bind as a recursive function:
%
\begin{code}
  _ùìë_ : Hefty H A ‚Üí (A ‚Üí Hefty H B) ‚Üí Hefty H B
  pure x               ùìë g = g x
  impure (op , k , œà)  ùìë g = impure (op , (_ùìë g) ‚àò k , œà)
\end{code}
\begin{code}[hide]
  _>>_ : Hefty H A ‚Üí Hefty H B ‚Üí Hefty H B
  m‚ÇÅ >> m‚ÇÇ = m‚ÇÅ ùìë Œª _ ‚Üí m‚ÇÇ

  hmap : (A ‚Üí B) ‚Üí Hefty H A ‚Üí Hefty H B
  hmap f (pure x)               = pure (f x)
  hmap f (impure (op , k , œà))  = impure (op , hmap f ‚àò k , œà)
\end{code}
%
The bind behaves similarly to the bind for \ad{Free}; i.e., \ab{m}~\af{ùìë}~\ab{g}
concatenates \ab{g} to all the leaves in the continuations (but not computation
parameters) of \ab{m}.

In \cref{sec:hefty-algebras} we show how to modularly elaborate higher-order
operations into more primitive algebraic effects and handlers (i.e.,
computations over \ad{Free}), by folding modular elaboration algebras
(\emph{hefty algebras}) over \ad{Hefty} trees.  First, we show (in
\cref{sec:lifting-algebraic-to-higher-order}) how \ad{Hefty} trees support
programming against an interface of both algebraic and higher-order operations.
We also address (in \cref{sec:hefty-monadic-bind}) the question of how to encode
effect signatures for higher-order operations whose computation parameters have
polymorphic return types, such as the highlighted \colorbox{gray!30}{\ab{A}}
below:
\begin{flalign*}
  \;\;\af{‚Äµcatch}~\as{:}~\ad{Hefty}~\ab{H}~\colorbox{gray!30}{\ab{A}}~\as{‚Üí}~\ad{Hefty}~\ab{H}~\colorbox{gray!30}{\ab{A}}~\as{‚Üí}~\ad{Hefty}~\ab{H}~\colorbox{gray!30}{\ab{A}}
\end{flalign*}


\subsection{Programs with Algebraic and Higher-Order Effects}
\label{sec:lifting-algebraic-to-higher-order}

Any algebraic effect signature can be lifted to a higher-order effect signature
with no fork (i.e., no computation parameters):
%
\begin{code}
  Lift : Effect ‚Üí Effect·¥¥
  Op·¥¥   (Lift Œî) = Op Œî
  Ret·¥¥  (Lift Œî) = Ret Œî
  Fork  (Lift Œî) = Œª _ ‚Üí ‚ä•
  Ty    (Lift Œî) = Œª()
\end{code}
%
Using this effect signature, and using higher-order effect row insertion
witnesses analogous to the ones we defined and used in \cref{sec:row-insertion},
the following smart constructor lets us represent any algebraic operation as a
\ad{Hefty} computation:
%
\begin{code}[hide]
  record _‚àô_‚âã_ (H‚ÇÅ H‚ÇÇ H : Effect·¥¥) : Set‚ÇÅ where
    field
      reorder : ‚àÄ {M X} ‚Üí ‚ü¶ H‚ÇÅ ‚àî H‚ÇÇ ‚üß·¥¥ M X ‚Üî ‚ü¶ H ‚üß·¥¥ M X

  _‚â≤·¥¥_ : (H‚ÇÅ H‚ÇÇ : Effect·¥¥) ‚Üí Set‚ÇÅ
  H‚ÇÅ ‚â≤·¥¥ H‚ÇÇ = ‚àÉ Œª H ‚Üí H‚ÇÅ ‚àô H ‚âã H‚ÇÇ

  postulate ‚â≤·¥¥-refl  : H ‚â≤·¥¥ H 
  postulate ‚â≤·¥¥-trans : H‚ÇÅ ‚â≤·¥¥ H‚ÇÇ ‚Üí H‚ÇÇ ‚â≤·¥¥ H‚ÇÉ ‚Üí H‚ÇÅ ‚â≤·¥¥ H‚ÇÉ
\end{code}
%
\begin{code}[hide]
  open _‚àô_‚âã_

  inj·¥¥À° : ‚àÄ {M X} ‚Üí ‚ü¶ H‚ÇÅ ‚üß·¥¥ M X ‚Üí ‚ü¶ H‚ÇÅ ‚àî H‚ÇÇ ‚üß·¥¥ M X
  inj·¥¥À° (op , k , œà) = inj‚ÇÅ op , k , œà
  
  inj·¥¥ : ‚¶É H‚ÇÅ ‚â≤·¥¥ H‚ÇÇ ‚¶Ñ ‚Üí ‚àÄ {M X} ‚Üí ‚ü¶ H‚ÇÅ ‚üß·¥¥ M X ‚Üí ‚ü¶ H‚ÇÇ ‚üß·¥¥ M X  
  inj·¥¥ {H‚ÇÇ = _} ‚¶É w ‚¶Ñ {M} {X} x = w .proj‚ÇÇ .reorder {M = M} {X = X} .Inverse.to (inj·¥¥À° {M = M} {X = X} x)
  
--   inj‚ñπ‚Çó  :  ‚¶É H ‚àº H‚ÇÄ ‚ñπ H‚Ä≤ ‚¶Ñ ‚Üí Op·¥¥ H‚ÇÄ  ‚Üí Op·¥¥ H
--   inj‚ñπ·µ£  :  ‚¶É H ‚àº H‚ÇÄ ‚ñπ H‚Ä≤ ‚¶Ñ ‚Üí Op·¥¥ H‚Ä≤  ‚Üí Op·¥¥ H
-- 
--   inj‚ñπ‚Çó ‚¶É insert ‚¶Ñ  = inj‚ÇÅ
--   inj‚ñπ‚Çó ‚¶É sift p ‚¶Ñ  = inj‚ÇÇ ‚àò inj‚ñπ‚Çó ‚¶É p ‚¶Ñ
-- 
--   inj‚ñπ·µ£ ‚¶É insert ‚¶Ñ  = inj‚ÇÇ
--   inj‚ñπ·µ£ ‚¶É sift p ‚¶Ñ  = [ inj‚ÇÅ , inj‚ÇÇ ‚àò inj‚ñπ·µ£ ‚¶É p ‚¶Ñ ]
-- 
-- 
--   inj‚ñπ‚Çó-ret‚â° : ‚¶É w : H ‚àº H‚ÇÄ ‚ñπ H‚Ä≤ ‚¶Ñ (op : Op·¥¥ H‚ÇÄ)
--              ‚Üí Ret·¥¥ H (inj‚ñπ‚Çó op) ‚â° Ret·¥¥ H‚ÇÄ op
--   inj‚ñπ‚Çó-ret‚â° ‚¶É insert ‚¶Ñ _  = refl
--   inj‚ñπ‚Çó-ret‚â° ‚¶É sift p ‚¶Ñ    = inj‚ñπ‚Çó-ret‚â° ‚¶É p ‚¶Ñ
-- 
--   inj‚ñπ·µ£-ret‚â° : ‚¶É p : H ‚àº H‚ÇÄ ‚ñπ H‚Ä≤ ‚¶Ñ (op : Op·¥¥ H‚Ä≤)
--             ‚Üí Ret·¥¥ H (inj‚ñπ·µ£ op) ‚â° Ret·¥¥ H‚Ä≤ op
--   inj‚ñπ·µ£-ret‚â° ‚¶É insert ‚¶Ñ op  = refl
--   inj‚ñπ·µ£-ret‚â° ‚¶É sift p ‚¶Ñ     = [ (Œª _ ‚Üí refl) , inj‚ñπ·µ£-ret‚â° ‚¶É p ‚¶Ñ ]
-- 
--   inj‚ñπ‚Çó-fork‚â° : ‚¶É w : H ‚àº H‚ÇÄ ‚ñπ H‚Ä≤ ‚¶Ñ (op : Op·¥¥ H‚ÇÄ)
--                 ‚Üí Fork H (inj‚ñπ‚Çó op) ‚â° Fork H‚ÇÄ op
--   inj‚ñπ‚Çó-fork‚â° ‚¶É insert ‚¶Ñ _  = refl
--   inj‚ñπ‚Çó-fork‚â° ‚¶É sift p ‚¶Ñ    = inj‚ñπ‚Çó-fork‚â° ‚¶É p ‚¶Ñ
-- 
--   inj‚ñπ·µ£-fork‚â° : ‚¶É p : H ‚àº H‚ÇÄ ‚ñπ H‚Ä≤ ‚¶Ñ (op : Op·¥¥ H‚Ä≤)
--                 ‚Üí Fork H (inj‚ñπ·µ£ op) ‚â° Fork H‚Ä≤ op
--   inj‚ñπ·µ£-fork‚â° ‚¶É insert ‚¶Ñ op  = refl
--   inj‚ñπ·µ£-fork‚â° ‚¶É sift p ‚¶Ñ     = [ (Œª _ ‚Üí refl) , inj‚ñπ·µ£-fork‚â° ‚¶É p ‚¶Ñ ]
-- 
--   inj‚ñπ‚Çó-prong‚â° : ‚¶É p : H ‚àº H‚ÇÄ ‚ñπ H‚Ä≤ ‚¶Ñ {op : Op·¥¥ H‚ÇÄ} (b : Op (Fork H (inj‚ñπ‚Çó op)))
--                 ‚Üí Ret (Fork H (inj‚ñπ‚Çó op)) b ‚â° Ret (Fork H‚ÇÄ op) (subst Op (inj‚ñπ‚Çó-fork‚â° ‚¶É p ‚¶Ñ op) b)
--   inj‚ñπ‚Çó-prong‚â° ‚¶É insert ‚¶Ñ op  = refl
--   inj‚ñπ‚Çó-prong‚â° ‚¶É p = sift p ‚¶Ñ {op} b = inj‚ñπ‚Çó-prong‚â° ‚¶É p ‚¶Ñ b
-- 
--   -- inj‚ñπ·µ£-prong‚â° : ‚¶É p : H ‚àº H‚ÇÄ ‚ñπ H‚Ä≤ ‚¶Ñ {op : Op H‚Ä≤} (b : Fork H (inj‚ñπ·µ£ op))
--   --               ‚Üí Prong H b ‚â° Prong H‚Ä≤ (subst id (inj‚ñπ·µ£-fork‚â° ‚¶É p ‚¶Ñ op) b)
--   -- inj‚ñπ·µ£-prong‚â° ‚¶É insert ‚¶Ñ op  = refl
--   -- inj‚ñπ·µ£-prong‚â° ‚¶É p = sift p ‚¶Ñ {inj‚ÇÅ x} b = refl
--   -- inj‚ñπ·µ£-prong‚â° ‚¶É p = sift p ‚¶Ñ {inj‚ÇÇ y} b = inj‚ñπ·µ£-prong‚â° ‚¶É p ‚¶Ñ b
-- 
-- 
--   proj-ret‚ñπ‚Çó : ‚¶É w : H ‚àº H‚ÇÄ ‚ñπ H‚Ä≤ ‚¶Ñ {op : Op·¥¥ H‚ÇÄ} ‚Üí Ret·¥¥ H (inj‚ñπ‚Çó op) ‚Üí Ret·¥¥ H‚ÇÄ op
--   proj-ret‚ñπ‚Çó ‚¶É w = insert ‚¶Ñ = id
--   proj-ret‚ñπ‚Çó ‚¶É w = sift w ‚¶Ñ = proj-ret‚ñπ‚Çó ‚¶É w ‚¶Ñ
--   
--   proj-ret‚ñπ·µ£ : ‚¶É w : H ‚àº H‚ÇÄ ‚ñπ H‚Ä≤ ‚¶Ñ {op : Op·¥¥ H‚Ä≤} ‚Üí Ret·¥¥ H (inj‚ñπ·µ£ op) ‚Üí Ret·¥¥ H‚Ä≤ op
--   proj-ret‚ñπ·µ£ ‚¶É w = insert ‚¶Ñ = id
--   proj-ret‚ñπ·µ£ ‚¶É w = sift w ‚¶Ñ {op = inj‚ÇÅ x} = id
--   proj-ret‚ñπ·µ£ ‚¶É w = sift w ‚¶Ñ {op = inj‚ÇÇ y} = proj-ret‚ñπ·µ£ ‚¶É w ‚¶Ñ
-- 
--   proj-fork‚ñπ‚Çó : ‚¶É w : H ‚àº H‚ÇÄ ‚ñπ H‚Ä≤ ‚¶Ñ {op : Op·¥¥ H‚ÇÄ}
--                 ‚Üí ((b : Op (Fork H‚ÇÄ op)) ‚Üí Hefty H (Ret (Fork H‚ÇÄ op) b))
--                 ‚Üí ((b : Op (Fork H (inj‚ñπ‚Çó op))) ‚Üí Hefty H (Ret (Fork H (inj‚ñπ‚Çó op)) b))
--   proj-fork‚ñπ‚Çó ‚¶É w ‚¶Ñ {op} f b  =
--     let x = f (subst Op (inj‚ñπ‚Çó-fork‚â° ‚¶É w ‚¶Ñ op) b) in
--     subst (Hefty _) (sym $ inj‚ñπ‚Çó-prong‚â° ‚¶É w ‚¶Ñ b) x
\end{code}
%
\begin{code}
  ‚Üë_ : ‚¶É w : Lift Œî ‚â≤·¥¥ H ‚¶Ñ ‚Üí (op : Op Œî) ‚Üí Hefty H (Ret Œî op)
\end{code}
\begin{code}[hide]
  ‚Üë_ op = impure (inj·¥¥ {M = Hefty _} (op , pure , Œª()))
\end{code}
%
Using this notion of lifting, \ad{Hefty} trees can be used to program against
interfaces of both higher-order and plain algebraic effects.


\subsection{Higher-Order Operations with Polymorphic Return Types}
\label{sec:hefty-monadic-bind}

Let us consider how to define \ad{Catch} as a higher-order effect.  Ideally, we
would define an operation that is parameterized by a return type of the branches
of a particular catch operation, as shown on the left, such that we can define
the higher-order effect signature on the right:\footnote{\textsf{\ab{d}} is for
  \textsf{\ab{dubious}}.}
%
\\
\begin{minipage}{0.495\linewidth}
\begin{code}
  data CatchOpÃÖ : Set‚ÇÅ where
    catchÃÖ : Set ‚Üí CatchOpÃÖ
\end{code}
\begin{code}[hide]
  record Effect·¥¥‚Öã : Set‚ÇÇ where
    field  Op·¥¥     : Set‚ÇÅ
           Ret·¥¥    : Op·¥¥ ‚Üí Set
           Fork    : Op·¥¥ ‚Üí Set                       -- New
           Ty      : {op : Op·¥¥} (œà : Fork op) ‚Üí Set  -- New
  open Effect·¥¥‚Öã
\end{code}
\end{minipage}%
\hfill\vline\hfill
\begin{minipage}{0.495\linewidth}
\begin{code}
  CatchÃÖ : Effect·¥¥‚Öã
  Op·¥¥    CatchÃÖ = CatchOpÃÖ
  Ret·¥¥   CatchÃÖ (catchÃÖ A)  = A
  Fork   CatchÃÖ (catchÃÖ A)  = Bool
  Ty     CatchÃÖ {catchÃÖ A} _  = A
\end{code}
\end{minipage}%
\\
The \aF{Fork} field on the right says that \ad{Catch} has two sub-computations
(since \ad{Bool} has two constructors), and that each computation parameter has
some return type \ab{A}.  However, the signature on the right above is not well
defined!

The problem is that, because \ad{CatchOpÃÖ} has a constructor that quantifies over
a type (\ad{Set}), the \ad{CatchOpÃÖ} type lives in \ad{Set‚ÇÅ}.  Consequently it
does not fit the definition of \ad{Effect·¥¥}, whose operations live in \ad{Set}.
There are two potential solutions to this problem: (1) increase the universe
level of \ad{Effect·¥¥} to allow \aF{Op·¥¥} to live in \ad{Set‚ÇÅ}; or (2) use a
\emph{universe of types}~\citep{martin-lof1984intuitionistic}.  Either
solution is applicable here; we choose type universes. 

A universe of types is a (dependent) pair of a syntax of types
(\aF{Ty}~\as{:}~\ad{Set}) and a semantic function
(\aF{‚ü¶\_‚üß·µÄ}~\as{:}~\aF{Ty}~\as{‚Üí}~\ad{Set}) defining the meaning of the syntax
by reflecting it into Agda's \ad{Set}:
%
\begin{code}
  record Univ : Set‚ÇÅ where
    field  Type  : Set
           ‚ü¶_‚üß·µÄ  : Type ‚Üí Set
\end{code}
\begin{code}[hide]
  open Univ ‚¶É ... ‚¶Ñ
\end{code}
%
\Cref{sec:higher-order-lambda} contains a concrete example usage this notion of type
universe.
Using type universes, we can parameterize the \ac{catch} constructor on the left
below by a \emph{syntactic type} \aF{Ty} of some universe \ab{u}, and use the
\emph{meaning of this type} (\aF{‚ü¶}~\ab{t}~\aF{‚üß·µÄ}) as the return type of the
computation parameters in the effect signature on the right below:
%
\begin{minipage}{0.495\linewidth}
\begin{code}
  data CatchOp ‚¶É u : Univ ‚¶Ñ : Set where
    catch : Type ‚Üí CatchOp
\end{code}
\end{minipage}
\hfill\vline\hfill
\begin{minipage}{0.495\linewidth}
\begin{code}
  Catch : ‚¶É u : Univ ‚¶Ñ ‚Üí Effect·¥¥
  Op·¥¥    Catch            = CatchOp
  Ret·¥¥   Catch (catch t)  = ‚ü¶ t ‚üß·µÄ
  Fork   Catch (catch t)  = Bool
  Ty     Catch {catch t}  = Œª _ ‚Üí ‚ü¶ t ‚üß·µÄ
\end{code}
\end{minipage}
\begin{code}[hide]
  ‚Äµcatch   : ‚¶É u : Univ ‚¶Ñ ‚¶É w : Catch ‚â≤·¥¥ H ‚¶Ñ {t : Type} 
           ‚Üí Hefty H ‚ü¶ t ‚üß·µÄ ‚Üí Hefty H ‚ü¶ t ‚üß·µÄ  ‚Üí Hefty H ‚ü¶ t ‚üß·µÄ
\end{code}
\begin{code}[hide]
  ‚Äµcatch {t = t} m‚ÇÅ m‚ÇÇ = impure (inj·¥¥ {M = Hefty _} {X = ‚ü¶ t ‚üß·µÄ} ((catch t) , pure , (if_then m‚ÇÅ else m‚ÇÇ)))
\end{code}
%
While the universe of types encoding restricts the kind of type that catch can
have as a return type, the effect signature is parametric in the universe.  Thus
the implementer of the \ad{Catch} effect signature (or interface) is free to
choose a sufficiently expressive universe of types.

\subsection{Hefty Algebras}
\label{sec:hefty-algebras}

As shown in \cref{sec:higher-order-effects}, the higher-order catch operation
can be encoded as a non-modular elaboration:
%
\begin{code}[hide]
  module _  ‚¶É w : Throw ‚â≤ Œî ‚¶Ñ where
    catch‚Öã : Free Œî A ‚Üí Free Œî A ‚Üí Free Œî A
\end{code}
\begin{code}
    catch‚Öã m‚ÇÅ m‚ÇÇ = (‚ôØ ((given hThrow handle m‚ÇÅ) tt)) ùìë‚Öã (maybe pure m‚ÇÇ)
\end{code}
\begin{code}[hide]
      where open FreeModule using () renaming (_ùìë_ to _ùìë‚Öã_)
            postulate instance foo : proj‚ÇÅ w ‚â≤ _ 
\end{code}
%
We can make this elaboration modular by expressing it as an \emph{algebra} over
\ad{Hefty} trees containing operations of the \ad{Catch} signature.  To this
end, we will use the following notion of hefty algebra (\ad{Alg·¥¥}) and fold (or
\emph{catamorphism}~\citep{DBLP:conf/fpca/MeijerFP91}, \af{cata·¥¥}) for
\af{Hefty}:
%
\begin{code}
  record Alg·¥¥ (H : Effect·¥¥) (F : Set ‚Üí Set) : Set‚ÇÅ where
    field alg  : ‚ü¶ H ‚üß·¥¥ F A ‚Üí F A
\end{code}
\begin{code}[hide]
  open Alg·¥¥
\end{code}
\begin{code}
  cata·¥¥ : (‚àÄ {A} ‚Üí A ‚Üí F A) ‚Üí Alg·¥¥ H F ‚Üí Hefty H A ‚Üí F A
  cata·¥¥ g a (pure x)               = g x
  cata·¥¥ g a (impure (op , k , œà))  = alg a (op , ((cata·¥¥ g a ‚àò k) , (cata·¥¥ g a ‚àò œà)))
\end{code}
%
Here \ad{Alg·¥¥} defines how to transform an \ac{impure} node of type
\ad{Hefty}~\ab{H}~\ab{A} into a value of type \ab{F}~\ab{A}, assuming we have
already folded the computation parameters and continuation into \ab{F} values.
A nice property of algebras is that they are closed under higher-order effect
signature sums:
%
\begin{code}[hide]
  infixr 12 _‚ãé_
\end{code}
\begin{code}
  _‚ãé_ : Alg·¥¥ H‚ÇÅ F ‚Üí Alg·¥¥ H‚ÇÇ F ‚Üí Alg·¥¥ (H‚ÇÅ ‚àî H‚ÇÇ) F
  alg (A‚ÇÅ ‚ãé A‚ÇÇ) (inj‚ÇÅ op , k , œà) = alg A‚ÇÅ (op , k , œà)
  alg (A‚ÇÅ ‚ãé A‚ÇÇ) (inj‚ÇÇ op , k , œà) = alg A‚ÇÇ (op , k , œà)
\end{code}
%
By defining elaborations as hefty algebras (below) we can compose them using \ad{\_‚ãé\_}.
%
\begin{code}
  Elaboration : Effect·¥¥ ‚Üí Effect ‚Üí Set‚ÇÅ
  Elaboration H Œî = Alg·¥¥ H (Free Œî)
\end{code}
%
An \af{Elaboration}~\ab{H}~\ab{Œî} elaborates higher-order operations of
signature \ab{H} into algebraic operations of signature \ab{Œî}.  Given an
elaboration, we can generically transform any hefty tree into more primitive
algebraic effects and handlers:
%
\begin{code}
  elaborate : Elaboration H Œî ‚Üí Hefty H A ‚Üí Free Œî A
  elaborate = cata·¥¥ pure
\end{code}


\begin{code}[hide]
module ElabModule where
  open FreeModule hiding (_ùìë_; _>>_)
  open HeftyModule hiding (_ùìë_; _>>_)
  open Alg·¥¥
  open Inverse ‚¶É ... ‚¶Ñ
  open import Relation.Binary using (Reflexive ; Transitive ; Symmetric)
  open import Function.Construct.Identity
  open import Function.Construct.Symmetry
  open import Function.Construct.Composition


  -- Analogous to container morphisms, morphisms of signatures are the natural
  -- transformations between their extension functors
  _‚Ü¶·¥¥_ : Effect·¥¥ ‚Üí Effect·¥¥ ‚Üí Set‚ÇÅ
  H‚ÇÅ ‚Ü¶·¥¥ H‚ÇÇ = ‚àÄ {F} ‚Üí ‚àÄ[ ‚ü¶ H‚ÇÅ ‚üß·¥¥ F ‚áí ‚ü¶ H‚ÇÇ ‚üß·¥¥ F ]  

  inj·¥¥ ≥ : H‚ÇÇ ‚Ü¶·¥¥ (H‚ÇÅ ‚àî H‚ÇÇ)
  inj·¥¥ ≥ (c , k , œà) = (inj‚ÇÇ c , k , œà)

  record _‚áø·¥¥_ (H‚ÇÅ H‚ÇÇ : Effect·¥¥) : Set‚ÇÅ where
    field
      equivalence·¥¥ : ‚àÄ F X ‚Üí ‚ü¶ H‚ÇÅ ‚üß·¥¥ F X ‚Üî ‚ü¶ H‚ÇÇ ‚üß·¥¥ F X
      -- TODO: do we require proofs of naturality for this relation as well? 


  open _‚áø·¥¥_ public 

  ‚áø·¥¥-refl : Reflexive _‚áø·¥¥_
  ‚áø·¥¥-refl =
    record { equivalence·¥¥ = Œª _ _ ‚Üí ‚Üî-id _ } 

  ‚áø·¥¥-sym : Symmetric _‚áø·¥¥_
  ‚áø·¥¥-sym eq =
    record { equivalence·¥¥ = Œª X Y ‚Üí ‚Üî-sym (eq .equivalence·¥¥ X Y) }

  ‚áø·¥¥-trans : Transitive _‚áø·¥¥_
  ‚áø·¥¥-trans eq‚ÇÅ eq‚ÇÇ =
    record { equivalence·¥¥ = Œª F X ‚Üí eq‚ÇÇ .equivalence·¥¥ F X ‚Üî-‚àò eq‚ÇÅ .equivalence·¥¥ F X }

  ‚ä•-sig : Effect·¥¥
  ‚ä•-sig = record
    { Op·¥¥  = ‚ä•
    ; Ret·¥¥ = Œª()
    ; Fork     = Œª()
    ; Ty  = (Œª where {op = ()})
    }

  swap-sig : (H‚ÇÅ ‚àî H‚ÇÇ) ‚Ü¶·¥¥ (H‚ÇÇ ‚àî H‚ÇÅ)
  swap-sig (inj‚ÇÅ c , k , œà) = (inj‚ÇÇ c , k , œà)
  swap-sig (inj‚ÇÇ c , k , œà) = (inj‚ÇÅ c , k , œà)

  swap-sig-involutive : {A : Set} ‚Üí (x : ‚ü¶ H‚ÇÅ ‚àî H‚ÇÇ ‚üß·¥¥ F A) ‚Üí swap-sig {x = A} (swap-sig {x = A} x) ‚â° x
  swap-sig-involutive (inj‚ÇÅ c , k , œà) = refl
  swap-sig-involutive (inj‚ÇÇ y , k , œà) = refl

  swap-sig-‚áø·¥¥ : (H‚ÇÅ ‚àî H‚ÇÇ) ‚áø·¥¥ (H‚ÇÇ ‚àî H‚ÇÅ)
  equivalence·¥¥ swap-sig-‚áø·¥¥ F X = record
    { to        = swap-sig {x = X}
    ; from      = swap-sig {x = X}
    ; to-cong   = Œª where refl ‚Üí refl
    ; from-cong = Œª where refl ‚Üí refl
    ; inverse   = (Œª where refl ‚Üí swap-sig-involutive _) , Œª where refl ‚Üí swap-sig-involutive _
    }

  assoc-sig ≥ : ((H‚ÇÅ ‚àî H‚ÇÇ) ‚àî H‚ÇÉ) ‚Ü¶·¥¥ (H‚ÇÅ ‚àî (H‚ÇÇ ‚àî H‚ÇÉ))  
  assoc-sig ≥ (inj‚ÇÅ (inj‚ÇÅ c) , k , œà) = (inj‚ÇÅ c , k , œà)
  assoc-sig ≥ (inj‚ÇÅ (inj‚ÇÇ c) , k , œà) = (inj‚ÇÇ (inj‚ÇÅ c) , k , œà)
  assoc-sig ≥ (inj‚ÇÇ c        , k , œà) = (inj‚ÇÇ (inj‚ÇÇ c) , k , œà)

  assoc-sigÀ° : (H‚ÇÅ ‚àî (H‚ÇÇ ‚àî H‚ÇÉ)) ‚Ü¶·¥¥ ((H‚ÇÅ ‚àî H‚ÇÇ) ‚àî H‚ÇÉ)
  assoc-sigÀ° (inj‚ÇÅ c        , k , œà) = (inj‚ÇÅ (inj‚ÇÅ c) , k , œà)
  assoc-sigÀ° (inj‚ÇÇ (inj‚ÇÅ c) , k , œà) = (inj‚ÇÅ (inj‚ÇÇ c) , k , œà)
  assoc-sigÀ° (inj‚ÇÇ (inj‚ÇÇ c) , k , œà) = (inj‚ÇÇ c , k , œà)

  assoc-sig-‚áø·¥¥ : ((H‚ÇÅ ‚àî H‚ÇÇ) ‚àî H‚ÇÉ) ‚áø·¥¥ (H‚ÇÅ ‚àî (H‚ÇÇ ‚àî H‚ÇÉ)) 
  equivalence·¥¥ assoc-sig-‚áø·¥¥ F X = record
    { to        = assoc-sig ≥ {x = X}
    ; from      = assoc-sigÀ° {x = X}
    ; to-cong   = Œª where refl ‚Üí refl
    ; from-cong = Œª where refl ‚Üí refl
    ; inverse   = (Œª where {x} refl ‚Üí assoc-inverse ≥ x) , Œª where {x} refl ‚Üí assoc-inverseÀ° x
    }
    where
      assoc-inverseÀ° : ‚àÄ {A : Set} ‚Üí (x : ‚ü¶ ((H‚ÇÅ ‚àî H‚ÇÇ) ‚àî H‚ÇÉ) ‚üß·¥¥ F A) ‚Üí assoc-sigÀ° (assoc-sig ≥ {x = A} x) ‚â° x
      assoc-inverseÀ° (inj‚ÇÅ (inj‚ÇÅ _) , _ , _) = refl
      assoc-inverseÀ° (inj‚ÇÅ (inj‚ÇÇ _) , _ , _) = refl
      assoc-inverseÀ° (inj‚ÇÇ _        , _ , _) = refl

      assoc-inverse ≥ : ‚àÄ {A : Set} ‚Üí (x : ‚ü¶ (H‚ÇÅ ‚àî (H‚ÇÇ ‚àî H‚ÇÉ)) ‚üß·¥¥ F A) ‚Üí assoc-sig ≥ (assoc-sigÀ° {x = A} x) ‚â° x
      assoc-inverse ≥ (inj‚ÇÅ _        , _ , _) = refl
      assoc-inverse ≥ (inj‚ÇÇ (inj‚ÇÅ _) , _ , _) = refl
      assoc-inverse ≥ (inj‚ÇÇ (inj‚ÇÇ _) , _ , _) = refl

  ‚äï·¥¥-congÀ° : H‚ÇÅ ‚áø·¥¥ H‚ÇÇ ‚Üí (H‚ÇÅ ‚àî H) ‚áø·¥¥ (H‚ÇÇ ‚àî H)
  equivalence·¥¥ (‚äï·¥¥-congÀ° {H‚ÇÅ}{H‚ÇÇ}{H} eq) F X = record
    { to        = to‚Ä≤ {F = F}
    ; from      = from‚Ä≤ {F = F}
    ; to-cong   = Œª where refl ‚Üí refl
    ; from-cong = Œª where refl ‚Üí refl
    ; inverse   = (Œª where refl ‚Üí cong-inverseÀ° _) , Œª where refl ‚Üí cong-inverse ≥ _
    }
    where
      to‚Ä≤ : (H‚ÇÅ ‚àî H) ‚Ü¶·¥¥ (H‚ÇÇ ‚àî H)
      to‚Ä≤ {F = F} {X} (inj‚ÇÅ c , k , œà) = inj·¥¥À° {X = X} (eq .equivalence·¥¥ F X .to (c , k , œà))
      to‚Ä≤ (inj‚ÇÇ c , k , œà) = (inj‚ÇÇ c , k , œà)

      from‚Ä≤ : (H‚ÇÇ ‚àî H) ‚Ü¶·¥¥ (H‚ÇÅ ‚àî H)
      from‚Ä≤ {F = F} {X} (inj‚ÇÅ c , k , œà) = inj·¥¥À° {X = X} (eq .equivalence·¥¥ F X .from (c , k , œà))
      from‚Ä≤ (inj‚ÇÇ c , k , œà) = (inj‚ÇÇ c , k , œà)

      cong-inverseÀ° : ‚àÄ {A} (x : ‚ü¶ H‚ÇÇ ‚àî H ‚üß·¥¥ F A) ‚Üí to‚Ä≤ {x = A} (from‚Ä≤ {x = A} x) ‚â° x 
      cong-inverseÀ° {A = A} (inj‚ÇÅ c , k , œà) = cong (inj·¥¥À° {X = A}) (eq .equivalence·¥¥ _ A .Inverse.inverse .proj‚ÇÅ refl)
      cong-inverseÀ° (inj‚ÇÇ c , k , œà) = refl

      cong-inverse ≥ : ‚àÄ {A} (x : ‚ü¶ H‚ÇÅ ‚àî H ‚üß·¥¥ F A) ‚Üí from‚Ä≤ {x = A} (to‚Ä≤ {x = A} x) ‚â° x
      cong-inverse ≥ {A = A} (inj‚ÇÅ c , k , œà) = cong (inj·¥¥À° {X = A}) (eq .equivalence·¥¥ _ _ .Inverse.inverse .proj‚ÇÇ refl)
      cong-inverse ≥ (inj‚ÇÇ c , k , œà) = refl


  ‚äï·¥¥-cong ≥ : H‚ÇÅ ‚áø·¥¥ H‚ÇÇ ‚Üí (H ‚àî H‚ÇÅ) ‚áø·¥¥ (H ‚àî H‚ÇÇ)
  equivalence·¥¥ (‚äï·¥¥-cong ≥ {H‚ÇÅ}{H‚ÇÇ}{H} eq) F X = record
    { to        = to‚Ä≤ {F = F}
    ; from      = from‚Ä≤ {F = F}
    ; to-cong   = Œª where refl ‚Üí refl
    ; from-cong = Œª where refl ‚Üí refl
    ; inverse   = (Œª where refl ‚Üí cong-inverseÀ° _) , Œª where refl ‚Üí cong-inverse ≥ _
    }
    where
      to‚Ä≤ : (H ‚àî H‚ÇÅ) ‚Ü¶·¥¥ (H ‚àî H‚ÇÇ)
      to‚Ä≤ (inj‚ÇÅ c , k , œà) = (inj‚ÇÅ c , k , œà) 
      to‚Ä≤ {F = F} (inj‚ÇÇ c , k , œà) = inj·¥¥ ≥ {F = F} (eq .equivalence·¥¥ F _ .to (c , k , œà))

      from‚Ä≤ : (H ‚àî H‚ÇÇ) ‚Ü¶·¥¥ (H ‚àî H‚ÇÅ)
      from‚Ä≤ (inj‚ÇÅ c , k , œà) = (inj‚ÇÅ c , k , œà) 
      from‚Ä≤ {F = F} (inj‚ÇÇ c , k , œà) = inj·¥¥ ≥ {F = F} (eq .equivalence·¥¥ F _ .from (c , k , œà))

      cong-inverseÀ° : ‚àÄ {A} (x : ‚ü¶ H ‚àî H‚ÇÇ ‚üß·¥¥ F A) ‚Üí to‚Ä≤ (from‚Ä≤ {F = F} x) ‚â° x 
      cong-inverseÀ° (inj‚ÇÅ c , k , œà) = refl  
      cong-inverseÀ° {A = A} (inj‚ÇÇ c , k , œà) = cong (inj·¥¥ ≥ {F = F}) (eq .equivalence·¥¥ _ A .Inverse.inverse .proj‚ÇÅ refl)

      cong-inverse ≥ : ‚àÄ {A} (x : ‚ü¶ H ‚àî H‚ÇÅ ‚üß·¥¥ F A)  ‚Üí from‚Ä≤ (to‚Ä≤ {F = F} x) ‚â° x
      cong-inverse ≥ (inj‚ÇÅ c , k , œà) = refl
      cong-inverse ≥ (inj‚ÇÇ c , k , œà) = cong (inj·¥¥ ≥ {F = F}) (eq .equivalence·¥¥ _ _ .Inverse.inverse .proj‚ÇÇ refl)

  ‚â≤·¥¥-left : H ‚â≤·¥¥ (H ‚àî H‚Ä≤)
  ‚â≤·¥¥-left = _ , (record { reorder = ‚Üî-id _ })

  ‚â≤·¥¥-right : ‚¶É H ‚â≤·¥¥ H‚ÇÇ ‚¶Ñ ‚Üí H ‚â≤·¥¥ (H‚ÇÅ ‚àî H‚ÇÇ)
  ‚â≤·¥¥-right {H} {H‚ÇÇ} {H‚ÇÅ} ‚¶É x , record { reorder = reorder } ‚¶Ñ =
    (H‚ÇÅ ‚àî _) , (record { reorder = Œª {M} {X} ‚Üí
      ‚äï·¥¥-cong ≥ (record { equivalence·¥¥ = Œª F X ‚Üí reorder {F} {X} }) .equivalence·¥¥ M X
        ‚Üî-‚àò (assoc-sig-‚áø·¥¥ .equivalence·¥¥ M X
            ‚Üî-‚àò (‚äï·¥¥-congÀ° swap-sig-‚áø·¥¥ .equivalence·¥¥ M X ‚Üî-‚àò ‚Üî-sym (assoc-sig-‚áø·¥¥ .equivalence·¥¥ M X))) })

  module _ where
    open FreeModule using (_ùìë_)

    eNil : Elaboration (Lift Nil) Œî
    alg eNil ()
\end{code}


\begin{example}[Elaboration for Output Censoring]
Let us return to the example from the introduction.
Here is the elaboration of the \ad{Censor} effect from \cref{fig:censor}.
%
\vspace{\abovedisplayskip}\noindent
{\AgdaNoSpaceAroundCode{}
\begin{code}
    eCensor : ‚¶É w : Output ‚â≤ Œî ‚¶Ñ ‚Üí Elaboration Censor Œî
\end{code}%
\begin{code}[hide]
    alg (eCensor ‚¶É w ‚¶Ñ) (censor f , k , m) = do
      (x , s) ‚Üê (‚ôØ ((given hOut handle m tt) tt))
      ‚Äµout (f s)
      k x
      where _>>=_ = _ùìë_
            _>>_ : Free Œî A ‚Üí Free Œî B ‚Üí Free Œî B
            _>>_ = Œª x m ‚Üí x ùìë Œª _ ‚Üí m
            instance _ = _ , ‚àô-comm (w .proj‚ÇÇ)

    module _ ‚¶É w : Output ‚â≤ Œî ‚¶Ñ where
      eCensor‚Öã : Elaboration Censor Œî
\end{code}%
\begin{code}
      alg eCensor‚Öã (censor f , k , œà) = do
        (x , s) ‚Üê ‚ôØ ((given hOut handle œà tt) tt)
        ‚Äµout (f s)
        k x
\end{code}
\begin{code}[hide]
        where _>>=_ = _ùìë_
              _>>_ : Free Œî A ‚Üí Free Œî B ‚Üí Free Œî B
              _>>_ = Œª x m ‚Üí x ùìë Œª _ ‚Üí m
              instance _ = _ , ‚àô-comm (w .proj‚ÇÇ)
\end{code}
}%
\par\addvspace{\belowdisplayskip}\noindent
This elaboration matches the $\Id{eCensor}$ elaboration discussed in \cref{sec:solving-the-modularity-problem}.
\end{example}

\begin{example}[Elaboration for Exception Catching]
We can also elaborate exception catching analogously to the non-modular \af{catch} elaboration discussed in \cref{sec:higher-order-effects} and in the beginning of this subsection:
\vspace{\abovedisplayskip}\noindent
{\AgdaNoSpaceAroundCode{}
\begin{code}
    eCatch : ‚¶É u : Univ ‚¶Ñ ‚¶É w : Throw ‚â≤ Œî ‚¶Ñ ‚Üí  Elaboration Catch Œî
\end{code}%
\begin{code}[hide]
    alg (eCatch ‚¶É w = w ‚¶Ñ) (catch t , k , œà) = 
      (‚ôØ ((given hThrow handle œà true) tt)) ùìë maybe k (œà false ùìë k)
      where instance _ = _ , ‚àô-comm (w .proj‚ÇÇ)
\end{code}%
\begin{code}[hide]
    module _ ‚¶É u : Univ ‚¶Ñ ‚¶É w : Throw ‚â≤ Œî ‚¶Ñ where
      eCatch‚Öã : Elaboration Catch Œî
\end{code}%
\begin{code}
      alg eCatch‚Öã (catch t , k , œà) = 
        (‚ôØ ((given hThrow handle œà true) tt)) ùìë maybe k (œà false ùìë k)
\end{code}%
\begin{code}[hide]
        where instance _ = _ , ‚àô-comm (w .proj‚ÇÇ)
\end{code}
}%
\par\addvspace{\belowdisplayskip}\noindent
The elaboration is essentially the same as its non-modular counterpart, except
that it now uses the universe of types encoding discussed in
\cref{sec:hefty-monadic-bind}, and that it now transforms syntactic
representations of higher-order operations instead.
%
\begin{code}[hide]
  eLift : ‚¶É Œî‚ÇÅ ‚â≤ Œî‚ÇÇ ‚¶Ñ ‚Üí Elaboration (Lift Œî‚ÇÅ) Œî‚ÇÇ
  alg (eLift ‚¶É w ‚¶Ñ) (op , k , œà) = impure (inj (op , k))

  module Transact where
    open HeftyModule using (_ùìë_; _>>_)

    private
      data Type : Set where
        unit  : Type
        num   : Type

    private instance
      TypeUniv : Univ
      Univ.Type TypeUniv = Type
      Univ.‚ü¶ TypeUniv ‚üß·µÄ unit  = ‚ä§
      Univ.‚ü¶ TypeUniv ‚üß·µÄ num   = ‚Ñï
\end{code}
%
Using this elaboration, we can, for example, run the following example program
involving the state effect from \cref{fig:state-effect-handler}, the throw
effect from \cref{sec:free-monad}, and the catch effect defined here:
%
\begin{code}
    transact  :  ‚¶É w‚Çõ  : Lift State ‚â≤·¥¥ H ‚¶Ñ ‚¶É w‚Çú : Lift Throw ‚â≤·¥¥ H ‚¶Ñ ‚¶É w : Catch ‚â≤·¥¥ H ‚¶Ñ
              ‚Üí  Hefty H ‚Ñï
    transact = do
      ‚Üë put 1
      ‚Äµcatch (do ‚Üë (put 2); (‚Üë throw) ùìë ‚ä•-elim) (pure tt)
      ‚Üë get
\end{code}
%
The program first sets the state to 1; then to 2; and then throws an exception.
The exception is caught, and control is immediately passed to the final
operation in the program which gets the state.  By also defining elaborations
for \ad{Lift} and \ad{Nil}, we can elaborate and run the program:
%
\begin{code}
    eTransact  :  ‚¶É _ : Throw ‚â≤ Œî ‚¶Ñ ‚¶É _ : State ‚â≤ Œî ‚¶Ñ
               ‚Üí  Elaboration (Catch ‚àî Lift Throw ‚àî Lift State ‚àî Lift Nil) Œî
    eTransact = eCatch ‚ãé eLift ‚ãé eLift ‚ãé eNil
\end{code}%
\vspace{-1em}%
\begin{code}[hide]
    module _ where
      private instance
        x‚ÇÄ : State ‚â≤ (State ‚äï Nil)
        x‚ÇÄ = ‚â≤-left
        x‚ÇÅ : State ‚â≤ (Throw ‚äï State ‚äï Nil)
        x‚ÇÅ = ‚â≤-right ‚¶É ‚â≤-left ‚¶Ñ
        x‚ÇÇ : Throw ‚â≤ (Throw ‚äï State ‚äï Nil)
        x‚ÇÇ = ‚â≤-left
        x‚ÇÉ : Lift State ‚â≤·¥¥ (Catch ‚àî Lift Throw ‚àî Lift State ‚àî Lift Nil)
        x‚ÇÉ = ‚â≤·¥¥-right ‚¶É ‚â≤·¥¥-right ‚¶É ‚â≤·¥¥-left ‚¶Ñ ‚¶Ñ
        x‚ÇÑ : Lift Throw ‚â≤·¥¥ (Catch ‚àî Lift Throw ‚àî Lift State ‚àî Lift Nil)
        x‚ÇÑ = ‚â≤·¥¥-right ‚¶É ‚â≤·¥¥-left ‚¶Ñ
        x‚ÇÖ : Catch ‚â≤·¥¥ (Catch ‚àî Lift Throw ‚àî Lift State ‚àî Lift Nil)
        x‚ÇÖ = ‚â≤·¥¥-left
\end{code}
\begin{code}
      test-transact : un (  (  given hSt
                               handle (  (  given hThrow
                                           handle (elaborate eTransact transact) )
                                         tt ) )
                            0 ) ‚â° (just 2 , 2)
      test-transact = refl
\end{code}
%
\label{ex:elab-catch}
\end{example}

\noindent The program in \cref{ex:elab-catch} uses a so-called \emph{global} interpretation of
state, where the \ac{put} operation in the ``try block'' of \ad{‚Äµcatch} causes
the state to be updated globally.  In \cref{sec:optional-transactional} we
return to this example and show how we can modularly change the elaboration of
the higher-order effect \ad{Catch} to yield a so-called \emph{transactional}
interpretation of state where the \ac{put} operation in the try block is rolled
back when an exception is thrown.


\subsection{Discussion and Limitations}
\label{sec:limitations}

Which (higher-order) effects can we describe using hefty trees and algebras?
Since the core mechanism of our approach is modular elaboration of higher-order
operations into more primitive effects and handlers, it is clear that hefty
trees and algebras are at least as expressive as standard algebraic effects.
The crucial benefit of hefty algebras over algebraic effects is that
higher-order operations can be declared and implemented modularly.  In this
sense, hefty algebras provide a modular abstraction layer over standard
algebraic effects that, although it adds an extra layer of indirection by
requiring both elaborations and handlers to give a semantics to hefty trees, is
comparatively cheap and implemented using only standard techniques such as
$F$-algebras.  As we show in \cref{sec:modular-reasoning}, hefty algebras also
let us define higher-order effect theories, akin to algebraic effect theories.

Conceptually, we expect that hefty trees can capture any \emph{monadic}
higher-order effect whose signature is given by a higher-order functor on
$\ad{Set}~‚Üí~\ad{Set}$.  \citet{DBLP:conf/popl/Filinski99} showed that any
monadic effect can be represented using continuations, and given that we can
encode the continuation monad using algebraic effects~\citep{SchrijversPWJ19} in
terms of the \emph{sub/jump} operations (\cref{sec:optional-transactional}) by
\citet{thielecke1997phd,DBLP:conf/csl/FioreS14}, it is possible to elaborate any
monadic effect into algebraic effects using hefty algebras.  The current Agda
implementation, though, is slightly more restrictive.  The type of effect
signatures, \ad{Effect·¥¥}, approximates the set of higher-order functors by
constructively enforcing that all occurrences of the computation type are
strictly positive.  Hence, there may be higher-order effects that are
well-defined semantically, but which cannot be captured in the Agda encoding
presented here.

Recent work by \citet{DBLP:journals/corr/abs-2302-01415} introduced a
higher-order free monad that coincides with our \ad{Hefty} type.  Their work
shows that hefty trees are, in fact, a free monad.  Furthermore, they
demonstrate that a range of existing effects frameworks from the literature can
be viewed as instances of hefty trees.

When comparing hefty trees to scoped effects, we observe two important
differences.  The first difference is that the syntax of programs with
higher-order effects is fundamentally more restrictive when using scoped
effects.  Specifically, as discussed at the end of \cref{sec:scoped-discussion},
scoped effects impose a restriction on operations that their computation
parameters must pass control directly to the continuation of the operation.
Hefty trees, on the other hand, do not restrict the control flow of computation
parameters, meaning that they can be used to define a broader class of
operations.  For instance, in \cref{sec:higher-order-lambda} we define a
higher-order effect for function abstraction, which is an example of an
operation where control does not flow from the computation parameter to the
continuation.

The second difference is that hefty algebras and scoped effects and handlers are
modular in different ways.  Scoped effects are modular because we can modularly
define, compose, and handle scoped operations, by applying scoped effect
handlers in sequence; i.e.:
%
\begin{equation*}
\ad{Prog}~\ab{Œî‚ÇÄ~Œ≥‚ÇÄ~A‚ÇÄ} \xrightarrow{h_1'}
\ad{Prog}~\ab{Œî‚ÇÅ~Œ≥‚ÇÅ~A‚ÇÅ} \xrightarrow{h_2'}
\cdots
\xrightarrow{h_n'}
\ad{Prog}~\ad{Nil}~\ad{Nil}~\ab{A‚Çô}
\tag{$\ddagger$}
\end{equation*}
%

As discussed in \cref{sec:weaving}, each handler application modularly
``weaves'' effects through sub-computations, using a dedicated \aF{glue}
function.%  Such weaving makes it possible to obtain different semantics by
applying different scoped effect handlers in different orders.

Hefty algebras, on the other hand, work by applying an elaboration
algebra assembled from modular components in one go.  The program resulting from
elaboration can then be handled using standard algebraic effect handlers; i.e.:
%
\begin{equation*}
\ad{Hefty}~\as{(}\ab{H‚ÇÄ}~\ad{‚àî}~\cdots~\ad{‚àî}~\ab{H‚Çò}\as{)}~\ab{A}
\xrightarrow{\af{elaborate}~\as{(}\ab{E‚ÇÄ}~\ad{‚ãé}~\cdots~\ad{‚ãé}~\ab{E‚Çò}\as{)}}
\ad{Free}~Œî~A \xrightarrow{h_1}
\cdots \xrightarrow{h_k}
\ad{Free}~\ad{Nil}~\ab{A‚Çñ}
\tag{\S}
\end{equation*}
%

The algebraic effect handlers $h_1,\ldots,h_k$ in (\S) serve the same purpose
as the scoped effect handlers $h_1',\ldots,h_n'$ in ($\dagger$); namely, to
provide a semantics of operations.  The order of handling is significant for
both algebraic effect handlers and for scoped effect handlers: applying the same
handlers in different orders may give a different semantics.

In contrast, the order that elaborations ($E_1,\ldots,E_m$) are composed in
(\S) does not matter.  Hefty algebras merely mediate higher-order
operations into ``first-order'' effect trees that then must be handled, using
standard effect handlers.  While scoped effects supports ``weaving'', standard
algebraic effect handlers do not.  This might suggest that scoped effects and
handlers are generally more expressive.  However, many scoped effects and
handlers can be emulated using algebraic effects and hanlders, by encoding
scoped operations as algebraic operations whose continuations encode a kind of
scoped syntax, inspired by \citet[\S{}7-9]{WuSH14}.\footnote{We suspect that it
is generally possible to encode scoped syntax and handlers in terms of algebraic
operations and handlers, but verifying this is future work.}  We illustrate how
in \cref{sec:optional-transactional}.





%%% Local Variables:
%%% reftex-default-bibliography: ("../references.bib")
%%% End:

